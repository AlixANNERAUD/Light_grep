options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = false;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = true;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
}

PARSER_BEGIN(AnalyseurSyntaxique)
package fr.insarouen.iti.compilation.lightgrep.analyseurs;

import java.util.ArrayList;
import java.util.List;
import java.util.Set;
import java.util.HashSet;

import fr.insarouen.iti.compilation.lightgrep.automate.Lettre;
import fr.insarouen.iti.compilation.lightgrep.automate.LettreException;
import fr.insarouen.iti.compilation.lightgrep.arbreSyntaxiqueAbstrait.ExpressionRationnelle;
import fr.insarouen.iti.compilation.lightgrep.arbreSyntaxiqueAbstrait.ExpressionRationnelleElementaire;
import fr.insarouen.iti.compilation.lightgrep.arbreSyntaxiqueAbstrait.ZeroAN;
import fr.insarouen.iti.compilation.lightgrep.arbreSyntaxiqueAbstrait.ZeroOuUn;
import fr.insarouen.iti.compilation.lightgrep.arbreSyntaxiqueAbstrait.UnAN;
import fr.insarouen.iti.compilation.lightgrep.arbreSyntaxiqueAbstrait.Concatenation;
import fr.insarouen.iti.compilation.lightgrep.arbreSyntaxiqueAbstrait.Union;


public class AnalyseurSyntaxique {
}

PARSER_END(AnalyseurSyntaxique)


TOKEN : {
    <CARACTERE_ECHAPE:"\\|" | "\\$" | "\\(" | "\\)" | "\\[" | "\\]" | "\\*" | "\\?" | "\\." | "\\-" | "\\$">
    | <OU:"|">
    | <PARAG:"(">
    | <PARAD:")">
    | <CROCHETG:"[">
    | <CROCHETD:"]">
    | <ZERO_N:"*">
    | <ZERO_UN:"?">
    | <UN_N:"+">
    | <JOKER:".">
    | <TIRET:"-">
    | <CIRCONFLEXE:"^">
    | <FIN_DE_LIGNE:"$">
    | <CARACTERE:["a"-"z","A"-"Z","0"-"9","!","\"","#","%","&",",","/",":",";",
            "<","=",">","?","@","^","_","`","{","|","}","~"]>
}

ExpressionRationnelle expressionRationnelle() throws LettreException :
// @u:start expressionRationnelle
{
  HashSet<ExpressionRationnelle> ensemble=null;
  ExpressionRationnelle expression1=null, expression2=null;
}{
  expression1=expressionsRationnellesConcatenees() ( <OU> expression2=expressionRationnelle() )? {
    ensemble = new HashSet<ExpressionRationnelle>();
    ensemble.add(expression1);
    if (expression2 != null) {
      ensemble.add(expression2);
    }
    return new Union(ensemble);
  }
}

// @u:end expressionRationnelle

ExpressionRationnelle expressionsRationnellesConcatenees()  throws LettreException :
// @u:start expressionRationnellesConcatenees
{
  ArrayList<ExpressionRationnelle> liste=null;
  ExpressionRationnelle expression1=null, expression2=null;
}
{
  expression1=expressionRationnelleBasique() ( expression2=expressionsRationnellesConcatenees() )? {
    liste=new ArrayList<ExpressionRationnelle>();
    if (expression2 != null) {
      if (expression2 instanceof Concatenation) {
        liste.addAll(((Concatenation)expression2).getExpressionsRationelles());
      } else {
        throw new LettreException("L'expression rationnelle n'est pas une concat√©nation : " + expression2.toString());
      }
    }
    liste.add(expression1);
    return new Concatenation(liste);
  }
}

// @u:end expressionRationnellesConcatenees

ExpressionRationnelle expressionRationnelleBasique() throws LettreException :
// @u:start expressionRationnelleBasique
{
  Token t = null;
  ExpressionRationnelle expression = null;
}{
  expression=expressionRationnelleElementaire() (t = <ZERO_N>|t = <ZERO_UN>|t = <UN_N>)? 
  {
    if (t != null) {
      switch (t.kind) {
        case ZERO_N:
          return new ZeroAN(expression);
        case ZERO_UN:
          return new ZeroOuUn(expression);
        case UN_N:
          return new UnAN(expression);
        default:
          throw new LettreException("Expression rationnelle basique incorrecte"); // Impossible car l'analyseur syntatique ne devrait pas accepter ce cas.
      }
    } else {
      return expression;
    }

  }
}

// @u:end expressionRationnelleBasique

ExpressionRationnelle expressionRationnelleElementaire() throws LettreException :
// @u:start expressionRationnelleElementaire
{
  Lettre l = null;
  Set<Lettre> ensemble = null;
  ExpressionRationnelle expression = null;
}{
  <PARAG> expression=expressionRationnelle() <PARAD> {return expression;}
  | l=lettre() {return new ExpressionRationnelleElementaire(l);}
  | <JOKER> {return new ExpressionRationnelleElementaire(new Lettre('.'));}
  | <CROCHETG> (<CIRCONFLEXE>)? ensemble=ensembleDeLettres() <CROCHETD> { return new ExpressionRationnelleElementaire(ensemble);}
}

// @u:end expressionRationnelleElementaire

Set<Lettre> ensembleDeLettres() throws LettreException :
// @u:start ensembleDeLettres

{
  Set<Lettre> elements = null, ensemble = null;
}{
  elements = elementsEnsembleDeLettres() ( ensemble = ensembleDeLettres() )?
  {
    if (ensemble != null) {
      elements.addAll(ensemble);
    }
    return elements;
  }
}

// @u:end ensembleDeLettres

Set<Lettre> elementsEnsembleDeLettres() throws LettreException :
// @u:start elementsEnsembleDeLettres

{
  Lettre l1 = null, l2 = null;
  Set<Lettre> ensemble = null;
}{
  l1=lettre () ( <TIRET> l2 = lettre() )? 
  {
    ensemble = new HashSet<Lettre>();
    ensemble.add(l1);
    if (l2 != null) {
      ensemble.add(l2);
    } 
    return ensemble;
  }
}

// @u:end elementsEnsembleDeLettres

Lettre lettre() :
// @u:start lettre
{
  Token t;
}{
  t=<CARACTERE> {return new Lettre(t.image.charAt(0));}
  | t=<CARACTERE_ECHAPE> {return new Lettre(t.image.charAt(0));}
}

// @u:end lettre
